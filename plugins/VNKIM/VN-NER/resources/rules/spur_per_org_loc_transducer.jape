/*
 *  spur_per_org_loc_transducer.jape
 *  Copyright 2004-2006 Truc Vien, HCMUT.
 */
Phase:	Spur_Per_Org_Loc_Transducer
Input: 	Sentence Split Token Number Pp Np FirstPerson LastPerson TempPerson TempLocation TempOrganization TempAbstract TempEvent NamedEntity
	JobTitle Title
	NLNC
	TTCT LHCT LHKD
	TTGDDT TDDT LHTC NNDT
	NLBCQNB NLCLBBD NLCLBTT NLCNPBK NLCQTP NLDCT NLDN NLHDND NLLDTT NLNOICAC NLQH NLSO NLTCNN NLTCQT NLTCTT NLTCTG NLTCXH NLTRT NLTTGDCK
	NLUBND NLVNC NLTCNC
	NLDT
	NLHTTN NLBC
	LR_TempPerson LR_TempLocation LR_TempOrganization LR_TempObject LR_TempAbstract LR_TempEvent
Options: control = appelt debug = no

///////////////////////////////////////////////////////////////
// MACRO definition
/////////////////////////////////////////////////////////////////

Macro: TITLE
(
   (  {Title}
      ({Title})?
   ) |
   (  {Title}
      ({JobTitle})?
   ) |
   ({JobTitle})
)


Macro: JOB_TITLE
(
   {JobTitle}
   (
      ({Token.string == "kiêm"})?
      {JobTitle}
   )?
)

///////////////////////////////////////////////////////////
// Person Rules

Rule:	Spur_PersonTitle_Org
Priority: 500
(
   (JOB_TITLE)
   (({NamedEntity} | {Np})+):orgName
   ({TempPerson}):personPure
):person
-->
{
	gate.AnnotationSet orgSet = (gate.AnnotationSet)bindings.get("orgName");
	gate.Annotation orgAnn = (gate.Annotation)orgSet.iterator().next();

	gate.FeatureMap features_org = Factory.newFeatureMap();
	features_org.put("class", "http://www.dit.hcmut.edu.vn/vnkim/vnkimo.rdfs#Tổ_chức");

	Long extendStart = orgSet.firstNode().getOffset();
	Long extendEnd = orgSet.lastNode().getOffset();

	gate.AnnotationSet tempSet = (gate.AnnotationSet)inputAS.getContained(extendStart, extendEnd);
	Iterator remIter = tempSet.iterator();
	java.util.HashSet remSet = new java.util.HashSet();

	while (remIter.hasNext())
	{
		Annotation ann = (Annotation)remIter.next();
		if (ann.getType().equals("NLNC") || ann.getType().equals("TTCT") || ann.getType().equals("LHCT") || ann.getType().equals("LHKD")
		 || ann.getType().equals("TTGDDT") || ann.getType().equals("TDDT") || ann.getType().equals("LHTC") || ann.getType().equals("NNDT")
		 || ann.getType().equals("NLTCNN"))
			remSet.add(ann);
		
	}
	if (remSet != null && remSet.size() > 0) annotations.removeAll(remSet);


	features_org.put("kind", "orgName");
	features_org.put("rule", "Spur_PersonTitle_Org");
	annotations.add(orgSet.firstNode(), orgSet.lastNode(), "TempOrganization", features_org);


	gate.AnnotationSet person = (gate.AnnotationSet)bindings.get("person");
	gate.Annotation personAnn = (gate.Annotation)person.iterator().next();
	gate.AnnotationSet personPure = (gate.AnnotationSet)bindings.get("personPure");
	gate.Annotation personPureAnn = (gate.Annotation)personPure.iterator().next();

	gate.FeatureMap features_per = personPureAnn.getFeatures();

	// get original string of the trusted text part
	String pure = doc.getContent().toString().substring(personPure.firstNode().getOffset().intValue(), personPure.lastNode().getOffset().intValue());
	features_per.put("originalName", pure);

	features_per.put("kind", "personPlus");
	features_per.put("rule", "Spur_PersonTitle_Org");
	annotations.add(person.firstNode(), person.lastNode(), "TempPerson", features_per);
}


Rule:	Spur_Country_Person
Priority: 500
(
   (TITLE)
   ({Token.string == "người"})?
   {TempLocation}
   (({NamedEntity} | {Np})+):personPure
):person
-->
{
	gate.AnnotationSet person = (gate.AnnotationSet)bindings.get("person");
	gate.Annotation personAnn = (gate.Annotation)person.iterator().next();
	gate.AnnotationSet personPure = (gate.AnnotationSet)bindings.get("personPure");
	gate.Annotation personPureAnn = (gate.Annotation)personPure.iterator().next();

	gate.FeatureMap features_per = Factory.newFeatureMap();
	features_per.put("class", "http://www.dit.hcmut.edu.vn/vnkim/vnkimo.rdfs#Con_người");

	// get original string of the trusted text part
	String pure = doc.getContent().toString().substring(personPure.firstNode().getOffset().intValue(), personPure.lastNode().getOffset().intValue());
	features_per.put("originalName", pure);

	features_per.put("kind", "personPlus");
	features_per.put("rule", "Spur_Country_Person");
	annotations.add(person.firstNode(), person.lastNode(), "TempPerson", features_per);
}



